#
# PASM toggle demo
#
# Copyright (c) 2011 Parallax, Inc.
# MIT Licensed.
#
# +--------------------------------------------------------------------
# Â¦  TERMS OF USE: MIT License
# +--------------------------------------------------------------------
# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files
# (the "Software"), to deal in the Software without restriction,
# including without limitation the rights to use, copy, modify, merge,
# publish, distribute, sublicense, and/or sell copies of the Software,
# and to permit persons to whom the Software is furnished to do so,
# subject to the following conditions:
#
# The above copyright notice and this permission notice shall be
# included in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
# +--------------------------------------------------------------------

#if defined(__PROPELLER2__)
		'' use PINB (hooked up to LEDs on the DE2-115)
		'' instead of PINA
#define dira dirb
#define outa pinb
#endif
		''
		'' Any section starting or ending in ".cog" is special to
		'' the linker: it knows that these need to be relocated to
		'' COG memory but loaded into hub. It also creates
		'' special names like _load_start_cogtoggle to tell where
		'' the code is in memory (so the main code knows how to
		'' load it into the cog).
		''
		'' The "ax" flag says the section will contain executable
		'' code. The assembler should figure this out on its own,
		'' but it never hurts to be explicit!
		''
		.section .cogtoggle, "ax"

		'' this code is written intending labels to be COG addresses
		'' (4 byte aligned) instead of HUB addresses (1 byte aligned),
		'' so we use the .cog_ram directive. This is useful for porting
		'' PASM code over, since PASM labels are always long addresses.
		'' Never use .pasm for LMM code executing from HUB RAM.
		.pasm

		'' This code should never be compressed for CMM mode, since
		'' it executes directly on the hardware
		.compress off
	
		'' load the pins and wait delay from the C
		'' variables "pins" and "wait_time" respectively
		'' note that C variables have an _ prepended
		'' also note that LMM code tends to be big,
		'' so a direct rdlong waitdelay, #_wait_time is
		'' probably not going to work (_wait_time is bigger than 511)

                rdlong  waitdelay, wait_addr ' read from hub to get
                rdlong  pins, pins_addr      ' the user's clkfreq delay and pins
                mov     dira, pins          ' set pins to output
		mov	outa, pins
	
#if defined(__PROPELLER2__)
		getcnt	nextcnt
		add	nextcnt,waitdelay
#else
		mov	nextcnt, cnt
                add     nextcnt, waitdelay
#endif

.loop
                xor     outa, pins          ' toggle pins
                waitcnt nextcnt, waitdelay  ' wait for half second
		rdlong  waitdelay, wait_addr ' update wait delay
                jmp     #.loop

pins            long    0
waitdelay       long    0                   ' read from hub to int
nextcnt         long    0

		'' addresses of C variables
		'' NOTE: we are in .pasm mode, so we have to
		'' prefix symbols in hub memory with "@"
		'' this would not be necessary in the default
		'' .gas mode
wait_addr	long	@_wait_time
pins_addr	long	@_pins
